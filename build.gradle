plugins {
    id 'java'
    id 'maven-publish'
    id "com.github.ben-manes.versions" version "0.52.0"
}

group 'de.longri'
version '1.1.0'

repositories {
    mavenCentral()
}

ext {
    junitVersion = '5.11.4'
}


tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}


dependencies {

    implementation 'com.google.guava:guava:33.4.0-jre'
    implementation 'org.apache.commons:commons-lang3:3.17.0'
    implementation 'org.jsoup:jsoup:1.19.1'

    implementation 'org.apache.lucene:lucene-analyzers-common:8.11.4'
    implementation 'org.apache.lucene:lucene-core:10.1.0'
    implementation "org.apache.lucene:lucene-queryparser:10.1.0"
    implementation "org.apache.lucene:lucene-backward-codecs:10.1.0"


    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")
}



test {
    useJUnitPlatform()
}

dependencyUpdates {
    resolutionStrategy {
        componentSelection {
            all {
                // Schließt alle Abhängigkeiten aus, die mit 'org.openjfx' (JavaFX) beginnen
                if (candidate.group == 'org.openjfx') {
                    reject('JavaFX dependencies are excluded')
                }
                if (candidate.group == 'org.junit.jupiter') {
                    reject('JUnit dependencies are excluded')
                }
            }
        }
    }
}

if (file('gradle.properties.local').exists()) {
    def props = new Properties()
    file('gradle.properties.local').withInputStream { props.load(it) }
    props.each { key, value -> project.ext.set(key, value) }
}

publishing {
    publications {
        maven(MavenPublication) {
            artifact("build/libs/" + rootProject.name + "-" + version + ".jar") {
                extension 'jar'
            }
        }
    }
    repositories {
        maven {
            name 'deploy'
            url deployRepoUrl
            allowInsecureProtocol = true
            credentials {
                username = deployRepoUsername
                password = deployRepoPassword
            }
        }
    }
}
task _FAT_JAR(dependsOn: test, type: Jar) {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    manifest {
        attributes 'Main-Class': 'de.longri'
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    with jar
}
task _CLEAN_TO_NEXUS {
}
task _BUILD_TO_NEXUS(dependsOn: _CLEAN_TO_NEXUS) {
}
task PUBLISH_TO_NEXUS(dependsOn: _BUILD_TO_NEXUS) {
    println "PUBLISH_TO_NEXUS version $version"
}
_CLEAN_TO_NEXUS.dependsOn(clean)
_BUILD_TO_NEXUS.dependsOn(_FAT_JAR)
PUBLISH_TO_NEXUS.mustRunAfter(_FAT_JAR)
PUBLISH_TO_NEXUS.dependsOn(publishAllPublicationsToDeployRepository)
publishMavenPublicationToDeployRepository.dependsOn(_FAT_JAR)